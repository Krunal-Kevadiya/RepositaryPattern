apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

def getProperty(String filename, String propName) {
    def propsFile = rootProject.file(filename)
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props[propName] != null) {
            return props[propName]
        } else {
            print("No such property " + propName + " in file " + filename);
        }
    } else {
        print(filename + " does not exist!")
    }
}

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.ownrepositarypatternsample"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
    }

    dataBinding {
        enabled = true
    }

    androidExtensions {
        experimental = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField "String", "TMDB_API_KEY", "\"${getProperty("local.properties", "tmdb_api_key")}\""
            it.buildConfigField "String", "API_BASE_URL", "\"${getProperty("local.properties", "api_base_url")}\""
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // support library
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "com.google.android.material:material:1.1.0-alpha06"
    implementation "androidx.palette:palette:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.recyclerview:recyclerview:1.0.0"

    // kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.21"
    implementation "org.jetbrains.anko:anko:0.10.1"

    // architecture components
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation "androidx.room:room-runtime:2.1.0-beta01"
    kapt "androidx.room:room-compiler:2.1.0-beta01"

    // dependency injection
    implementation "org.koin:koin-android-architecture:0.9.1"
    implementation "org.koin:koin-android:2.0.0-GA"
    implementation "org.koin:koin-androidx-scope:2.0.0-GA"
    implementation "org.koin:koin-androidx-viewmodel:2.0.0-GA"

    // network
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.squareup.okhttp3:okhttp:3.14.1"
    implementation "com.squareup.okhttp3:logging-interceptor:3.14.1"

    // glide
    implementation "com.github.bumptech.glide:glide:4.9.0"
    implementation "com.github.florent37:glidepalette:2.1.2"
    kapt "com.github.bumptech.glide:compiler:4.9.0"

    // adapter
    implementation "com.github.skydoves:baserecyclerviewadapter:0.1.1"

    // debugging
    implementation "com.jakewharton.timber:timber:4.7.1"
    implementation "com.facebook.stetho:stetho:1.5.0"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"

    // custom view
    implementation "devlight.io:navigationtabbar:1.2.5"
    implementation "co.lujun:androidtagview:1.1.4"
    implementation "com.ms-square:expandableTextView:0.1.4"

    implementation "com.github.Krunal-Kevadiya:kotlin-common:1.1.5"
}
